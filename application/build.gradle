plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'maven-publish'
    id 'signing'
    id 'org.unbroken-dome.test-sets' version '3.0.1'
}

apply plugin: "jacoco"
jacoco {
    toolVersion = "0.8.5"
}

application {
    mainClassName = "application.QontractApplication"
}

jar {
    from '../version.properties'
}

shadowJar {
    archiveFileName = "qontract.${archiveExtension.get()}"
    from '../version.properties'
}

repositories {
    mavenLocal()
//    maven {
//        url = 'https://jcenter.bintray.com'
//    }

    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
}

def ktor_version = "1.4.3"
def junit_version = "5.7.1"
def jgit_version = "5.11.0.202103091610-r"

dependencies {
    implementation('info.picocli:picocli-spring-boot-starter:4.6.1') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
    implementation 'info.picocli:picocli:4.6.1'
    implementation "io.ktor:ktor-client-core-jvm:${ktor_version}"
    implementation "io.ktor:ktor-network-tls:$ktor_version"
    implementation "io.ktor:ktor-network-tls-certificates:$ktor_version"

    implementation 'org.junit.platform:junit-platform-launcher:1.7.1'
    implementation 'org.junit.platform:junit-platform-reporting:1.7.1'

    implementation "org.eclipse.jgit:org.eclipse.jgit:$jgit_version"
    implementation "org.eclipse.jgit:org.eclipse.jgit.ssh.apache:$jgit_version"

    implementation 'org.slf4j:slf4j-nop:1.7.30'
    implementation 'org.apache.kafka:kafka-clients:2.7.0'

    implementation 'org.apache.ant:ant-junit:1.10.9'

    implementation(project(':core'))
    implementation(project(':junit5-support'))

    implementation "io.ktor:ktor-client-cio:$ktor_version"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"

    testImplementation('org.springframework.boot:spring-boot-starter-test:2.4.3') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude module: 'mockito-core'
    }
    testImplementation 'io.mockk:mockk:1.10.6'
    testImplementation('com.ninja-squad:springmockk:3.0.1') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'com.ginsberg:junit5-system-exit:1.1.1'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}

group = 'run.qontract'

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

testSets {
    acceptanceTest
}